//command 0 to 15on Serial and Serial1
//config command: pauto<on/off>wait<on/off>time<0 to 99999999>@
//arduino - parallel port pins: 37-D0, 36-D1, ...,30-D7
//serial - arduino pins: 
//Serial set pin 34,35,36,37 ; send 8 via Serial set 1000 on pin 34,..,37
//Serial1 set pin 30,31,32,33 ; send 6 via Serial set 0110 on pin 30,..,33 

#include <EEPROM.h>
#include "Arduino_LED_Matrix.h"

ArduinoLEDMatrix matrix;
uint8_t frame1[8][12] = {
  { 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 },
  { 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 },
  { 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 },
  { 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 },
  { 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 },
  { 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 },
  { 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 },
  { 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 }
  };
  uint8_t frame2[8][12] = {
  { 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1 },
  { 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1 },
  { 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1 },
  { 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1 },
  { 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1 },
  { 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1 },
  { 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1 },
  { 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1 }
  };

struct Settings {
    uint16_t signature;     // validation field
    unsigned long duration;
    bool waitMode = false;
    bool autoMode = false;
};

const uint16_t CONFIG_SIGNATURE = 0xBEEF;
Settings config;


void setup() {
    Serial.begin(115200);
    Serial.setTimeout(7200000); // Timeout di 2h
    Serial1.begin(115200);
    Serial1.setTimeout(7200000); // Timeout di 2h

    EEPROM.get(0, config);
    if (config.signature != CONFIG_SIGNATURE) {
        config.signature = CONFIG_SIGNATURE;
        config.duration = 1000;
        config.waitMode = false;
        config.autoMode = false;
        EEPROM.put(0, config);
    }

     for (int i = 2; i < 10; i++) {
      pinMode(i, OUTPUT);  
      digitalWrite(i,LOW);   
    }
    //optional strobe pin, connect pin1 on the parallel port
    //pinMode(12, OUTPUT); //probe pin
    //digitalWrite(12,HIGH);

    pinMode(13, OUTPUT); //integrated led pin
    digitalWrite(13,LOW);

    matrix.begin();
    matrix.clear();

    Serial.println("Ciao 115200 baud ");
}

void SetParallelPort(String bin2, String bin1) {
  // Unisce i due nibble per formare un byte binario
  String fullBinary = bin2 + bin1;
  
  // Converte la stringa binaria in un byte
  byte byteValue = strtol(fullBinary.c_str(), NULL, 2);

  // Scrive ogni singolo bit sui pin D2–D9 (bit 0 → D2, bit 7 → D9)
  digitalWrite(2, bitRead(byteValue, 0));
  digitalWrite(3, bitRead(byteValue, 1));
  digitalWrite(4, bitRead(byteValue, 2));
  digitalWrite(5, bitRead(byteValue, 3));
  //bin2
  digitalWrite(6, bitRead(byteValue, 4));
  digitalWrite(7, bitRead(byteValue, 5));
  digitalWrite(8, bitRead(byteValue, 6));
  digitalWrite(9, bitRead(byteValue, 7));

  //arduino LED (optional)
  //digitalWrite(13, bitRead(byteValue, 0));

  updateMatrixRowFromNibbles(bin2, bin1);
}

void updateMatrixRowFromNibbles(String bin2, String bin1) {
    // Assumiamo bin2 e bin1 siano stringhe binarie da 4 caratteri ciascuna

  // Offset per centrare i bit sulla riga da 12 colonne
  // bin2 parte da colonna 2, bin1 da colonna 6
  frame1[0][0] = bin2.charAt(0) == '1' ? 1 : 0;
  frame1[0][1] = bin2.charAt(1) == '1' ? 1 : 0;
  frame1[0][2] = bin2.charAt(2) == '1' ? 1 : 0;
  frame1[0][3] = bin2.charAt(3) == '1' ? 1 : 0;

  frame1[0][8] = bin1.charAt(0) == '1' ? 1 : 0;
  frame1[0][9] = bin1.charAt(1) == '1' ? 1 : 0;
  frame1[0][10] = bin1.charAt(2) == '1' ? 1 : 0;
  frame1[0][11] = bin1.charAt(3) == '1' ? 1 : 0;
  matrix.renderBitmap(frame1, 8, 12);
  Serial.print("b1: ");Serial.println(bin1);
  Serial.print("b2: ");Serial.println(bin2);
  
}



bool isFirstChar01(char c) {
    return (c >= '0' && c <= '1') ;
}
bool isSecondChar05(char c) {
    return (c >= '0' && c <= '5') ;

}

String readHexCharFromSerial(HardwareSerial &serialx) {
    if (serialx.available() >= 2) {
        char firstChar = serialx.peek();
        if (firstChar=='p') {return "-1";}
        char c1 = serialx.read();
        if (!(isFirstChar01(c1))) {serialx.read(); return "-1"; }
        char c2 = serialx.read();
        if (c1=='0') {
            if (!isdigit(c2)) {serialx.read(); return "-1"; }
            return String(String(c1) + String(c2));
        }
        if (c1=='1') {
            if (!(isSecondChar05(c2))) {serialx.read(); return "-1"; }
            return String(String(c1) + String(c2));
        }
       
    }
    return "-1";
}

String convertToBinary4Bit(String decvalue) {
  int number = decvalue.toInt();               // Convert the string to an integer
  String binary = String(number, BIN);     // Convert the number to a binary string

  // Pad with leading zeros if needed
  while (binary.length() < 4) {
    binary = "0" + binary;
  }

  return binary;  // Return the 4-bit binary string
}

String NibbleToBinaryString(String hexChar) {
    int value = (int) strtol(hexChar.c_str(), NULL, 16);
    String binStr = "";
    for (int i = 3; i >= 0; i--) {
        binStr += bitRead(value, i);
    }
    return binStr;
}

unsigned long uno;
unsigned long due;

String trig1 = "-1";
String trig2 = "-1";
String bin1 = "0000";
String bin2 = "0000";

unsigned long startTime1 = 0;
unsigned long startTime2 = 0;
bool timer1 = false;
bool timer2 = false;
bool received1 = false;
bool received2 = false;

void liveConfiguration(HardwareSerial &serialx) {
    char firstChar = serialx.peek();
    firstChar = tolower(firstChar);

    if (firstChar == 'p') {
        String cmd = serialx.readStringUntil('@');
        cmd.toLowerCase();
        serialx.println(cmd);

        if (cmd.indexOf("autoon") >= 0) {
            config.autoMode = true;
        } else if (cmd.indexOf("autooff") >= 0) {
            config.autoMode = false;
        }

        if (cmd.indexOf("waiton") >= 0) {
            config.waitMode = true;
        } else if (cmd.indexOf("waitoff") >= 0) {
            config.waitMode = false;
        }

        int tIndex = cmd.indexOf("time");
        //Serial.println(tIndex);
        if (tIndex >= 0) {
            String tPart = cmd.substring(tIndex + 4);
            config.duration = tPart.toInt();
        }
        EEPROM.put(0, config);


        matrix.renderBitmap(frame2, 8, 12);
        delay(1000);
        matrix.clear();

        serialx.println("---- Comando ricevuto ----");
        serialx.print("autoMode: ");
        serialx.println(config.autoMode ? "ON" : "OFF");
        serialx.print("waitMode: ");
        serialx.println(config.waitMode ? "ON" : "OFF");
        serialx.print("duration: ");
        serialx.println(config.duration);
        serialx.println("--------------------------");
    }
}

void serialWaitModeAutoOnOff() {
     
    trig1 = readHexCharFromSerial(Serial);
    if ((trig1 != "-1") && (received1 == false)) {
        bin1 = convertToBinary4Bit(trig1);
        received1 = true;
    }

    trig2 = readHexCharFromSerial(Serial1);
    if ((trig2 != "-1") && (received2 == false)) {
        bin2 = convertToBinary4Bit(trig2);
        received2 = true;
    }

    if ((received1 == true) && (received2 == true)) {
        SetParallelPort(bin1,bin2);
        received1 = false;
        received2 = false;

        if (config.autoMode == true) {
            unsigned long duration = config.duration;
            duration = duration * 1000;
            unsigned long startTime = micros();

            if (duration <= 16383) {
                delayMicroseconds(duration);
            } else {
                while (micros() - startTime < duration);
            }

            SetParallelPort("0000","0000");
        }
    }
}

void serialNoWaitModeAutoModeOff() {
    trig1 = readHexCharFromSerial(Serial);
    trig2 = readHexCharFromSerial(Serial1);

    if (trig1 != "-1") {
        bin1 = convertToBinary4Bit(trig1);
        SetParallelPort(bin2,bin1);
    }
    if (trig2 != "-1") {
        bin2 = convertToBinary4Bit(trig2);
        SetParallelPort(bin2,bin1);
    }
    

    //SetParallelPort(bin2,bin1);
}

void serialNoWaitModeAutoModeOn() {
    trig1 = readHexCharFromSerial(Serial);
    unsigned long duration = config.duration;
    duration = duration * 1000;

    if ((timer1 == true) && (trig1 == "-1") && (micros() - startTime1 >= duration)) {
        trig1 = "0";
        timer1 = false;
    }

    trig2 = readHexCharFromSerial(Serial1);
    if ((timer2 == true) && (trig2 == "-1") && (micros() - startTime2 >= duration)) {
        trig2 = "0";
        timer2 = false;
    }

    if (trig1 != "-1") {
        bin1 = convertToBinary4Bit(trig1);
        Serial.println(trig1);
        if (trig1 != "0") {
            timer1 = true;
            startTime1 = micros();
        }
        SetParallelPort(bin1,bin2);
    }

    if (trig2 != "-1") {
        bin2 = convertToBinary4Bit(trig2);
        if (trig2 != "0") {
            timer2 = true;
            startTime2 = micros();
        }
        SetParallelPort(bin1,bin2);
    }
}

void loop() {
   
    if (Serial.available()>0)  {
        liveConfiguration(Serial);
    }
    if (Serial1.available()>0)  {
        liveConfiguration(Serial1);
    }
        
    if (config.waitMode == true) {
            serialWaitModeAutoOnOff();
    }

    if ((config.waitMode == false) && (config.autoMode == false)) {
            serialNoWaitModeAutoModeOff();
    }
    

    if ((config.waitMode == false) && (config.autoMode == true)) {
        serialNoWaitModeAutoModeOn();
    }
}
